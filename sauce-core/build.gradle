apply plugin: 'java'

ext.localReleaseDest = "${buildDir}/release/${VERSION_NAME}"

group = POM_GROUP_ID
version = VERSION_NAME

sourceCompatibility = 1.7
targetCompatibility = 1.7

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:1.1.0'
}

task javadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    options.encoding = "UTF-8"
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

apply from: '../dev_publish.gradle'

// jacoco start
apply plugin: 'jacoco'

jacoco {
    toolVersion "0.7.1.201405082137"
}

jacocoTestReport {
    classDirectories = fileTree(
            dir: "${buildDir}/classes/main",
            exclude: [
                    '**/R*.class',
                    '**/BuildConfig*',
                    '**/*$*.class',
                    '**/*$*.java']
    )
    additionalSourceDirs = files([
            "src/main/java"
    ])

    executionData = files("${buildDir}/jacoco/test.exec")

    reports {
        xml.enabled = true
        csv.enabled = true
        xml {
            destination "${buildDir}/reports/jacoco/${project.name}/jacocoTestReport.xml"
        }
        html {
            destination "${buildDir}/reports/jacoco/${project.name}"
        }
    }
}

bintray {
    user = BINTRAY_USER_NAME
    key = BINTRAY_API_KEY
    pkg {
        repo = 'maven'
        name = POM_NAME
        desc = POM_DESCRIPTION
        websiteUrl = POM_URL
        issueTrackerUrl = POM_URL + '/issues'
        vcsUrl = POM_SCM_URL
        licenses = [POM_LICENCE_NAME]
        publicDownloadNumbers = true
    }
    version {
        name = VERSION_NAME
        //Optional configuration for GPG signing
        gpg {
            sign = true
            passphrase = 'passphrase' //Optional. The passphrase for GPG signing'
        }
        //Optional configuration for Maven Central sync of the version
        mavenCentralSync {
            sync = true
            user = SONATYPE_USERNAME
            password = SONATYPE_PASSWORD
        }
    }
//    publications = ['MyPublication']
    configurations = ['archives']
}

//publishing {
//    publications {
//        MyPublication(MavenPublication) {
//            from components.java
//            groupId 'org.jfrog.gradle.sample'
//            artifactId 'gradle-project'
//            version '1.1'
//        }
//    }
//}